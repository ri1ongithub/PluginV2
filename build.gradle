plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "io.papermc.paperweight.userdev" version "2.0.0-beta.16"
}

def getLatestGitTag() {
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--abbrev=0'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (Exception e) {
        return "unknown" // Default value if no tags exist
    }
}

// Versioning
def tag = System.getenv("TAG") ?: getLatestGitTag()
def isSnapshot = System.getenv('SNAPSHOT')?.toBoolean() ?: false
def gitHash = System.getenv('GITHUB_SHA') ?: 'local'

version = isSnapshot ? "$tag-SNAPSHOT-${gitHash.take(7)}" : (System.getenv("TAG") ? tag : "dev-$tag")
group = 'fr.openmc'

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url "https://mvn.mathiasd.fr/releases"
    }
    maven {
        url "https://maven.enginehub.org/repo/"
    }
    maven {
        url "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
    maven {
        url "https://repo.dmulloy2.net/repository/public/"
    }
    maven {
        url "https://jitpack.io" // Toujours Ã  la fin
    }
}

dependencies {
    paperweight.paperDevBundle '1.21.5-R0.1-SNAPSHOT'
    compileOnly 'com.github.LoneDev6:API-ItemsAdder:3.6.3-beta-14'
    compileOnly 'net.luckperms:api:5.4'
    compileOnly 'me.clip:placeholderapi:2.11.6'
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.9'
    compileOnly 'com.github.dmulloy2:ProtocolLib:-SNAPSHOT'

    implementation 'org.jetbrains:annotations:24.1.0'
    implementation 'com.github.Revxrsal.Lamp:common:3.2.1'
    implementation 'com.github.Revxrsal.Lamp:bukkit:3.2.1'
    implementation 'net.raidstone:wgevents:1.18.1'

    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'
    testCompileOnly 'org.projectlombok:lombok:1.18.34'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'

    testImplementation 'org.slf4j:slf4j-simple:2.0.16'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.mockbukkit.mockbukkit:mockbukkit-v1.21:4.46.4'
    testImplementation 'com.h2database:h2:2.3.232'
}

def targetJavaVersion = 21
java {
    sourceCompatibility = targetJavaVersion
    targetCompatibility = targetJavaVersion
}

paperweight {
  addServerDependencyTo = configurations.named(JavaPlugin.COMPILE_ONLY_CONFIGURATION_NAME).map { [it] as Set }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.release = targetJavaVersion
}

processResources {
    filteringCharset = 'UTF-8'

    def pluginProps = ["version": version]
    inputs.properties(pluginProps)
    filesMatching('plugin.yml') {
        expand(pluginProps)
    }
}

tasks.jar {
    manifest {
        attributes 'GIT-COMMIT': gitHash
    }
    destinationDirectory.set(file('./builds/'))
}

tasks.clean {
    delete 'builds'
}

tasks.shadowJar {
    destinationDirectory.set(file('./builds/'))
    archiveFileName.set('OpenMC.jar')
}

tasks.test {
    useJUnitPlatform()
}
